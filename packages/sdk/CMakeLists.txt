
# SDK Source & headers
FILE(GLOB_RECURSE sdkTestSources tests/*.cpp)
FILE(GLOB_RECURSE sdkSources src/*.cpp)
FILE(GLOB_RECURSE sdkHeaders include/*.h)
FILE(GLOB_RECURSE sdkInternalHeaders src/*.h)

# Create libraries
# SETUP_LIB_EXPORTS(${sdkTarget} ${sdkTargetStatic} sdkSources sdkInternalHeaders sdkHeaders)
SET(msvcRT $<TARGET_PROPERTY:MSVC_RUNTIME_LIBRARY>)

SET(msvcRTMtdStatic $<STREQUAL:${msvcRT},MultiThreadedDebug>)
SET(msvcRTMtStatic $<STREQUAL:${msvcRT},MultiThreaded>)

SET(msvcRTMtdDll $<STREQUAL:${msvcRT},MultiThreadedDebugDLL>)
SET(msvcRTMtDll $<STREQUAL:${msvcRT},MultiThreadedDLL>)

SET(backportMSVCRuntime $<VERSION_LESS:${CMAKE_VERSION},3.15>)

ADD_LIBRARY(${sdkTarget} ${IRSDKCPP_LIB_TYPE} ${sdkSources} ${sdkHeaders} ${exportHeader})
ADD_LIBRARY(irsdkcpp::${sdkTarget} ALIAS ${sdkTarget})

#GENERATE_EXPORT_HEADER(${sdkTarget} BASE_NAME ${macroBaseName})

TARGET_SOURCES(
  ${sdkTarget}
  PUBLIC
  FILE_SET publicHeaders
  TYPE HEADERS
  BASE_DIRS ${sdkIncludeDir}
  FILES
  ${sdkHeaders}
)

SET_PROPERTY(TARGET ${sdkTarget}
  PROPERTY MSVC_RUNTIME_LIBRARY ${CMAKE_MSVC_RUNTIME_LIBRARY})
SET_PROPERTY(TARGET ${sdkTarget}
  PROPERTY CXX_STANDARD_REQUIRED ON)

IF(NOT IRSDKCPP_BUILD_SHARED)
  SET_PROPERTY(TARGET ${sdkTarget} PROPERTY POSITION_INDEPENDENT_CODE ${IRSDKCPP_ENABLE_PIC})
ENDIF()

TARGET_INCLUDE_DIRECTORIES(${sdkTarget}
  PUBLIC
  $<BUILD_INTERFACE:${sdkIncludeDir}>
  $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
  PRIVATE
  $<BUILD_INTERFACE:${sdkPrivateIncludeDir}>)

TARGET_COMPILE_OPTIONS(${sdkTarget}
  PRIVATE
  $<$<AND:${backportMSVCRuntime},${msvcRTMtdStatic}>:-MTd>
  $<$<AND:${backportMSVCRuntime},${msvcRTMtStatic}>:-MT>
  $<$<AND:${backportMSVCRuntime},${msvcRTMtdDll}>:-MDd>
  $<$<AND:${backportMSVCRuntime},${msvcRTMtDll}>:-MD>

  # /wd4127 = disable warning C4127 "conditional expression is constant"
  # http://msdn.microsoft.com/en-us/library/6t66728h.aspx
  # /wd4355 = disable warning C4355 "'this' : used in base member initializer list
  # http://msdn.microsoft.com/en-us/library/3c594ae3.aspx
  $<$<CXX_COMPILER_ID:MSVC>:/W3 /wd4127 /wd4355>)

TARGET_COMPILE_DEFINITIONS(${sdkTarget}
  PUBLIC
  $<$<BOOL:${IRSDKCPP_BUILD_SHARED}>:IRSDKCPP_SHARED_LIB>
  $<$<NOT:$<BOOL:${IRSDKCPP_BUILD_SHARED}>>:IRSDKCPP_STATIC_LIB>
  PRIVATE
  $<${buildWindowsDLL}:IRSDKCPP_DLL>)

TARGET_COMPILE_DEFINITIONS(
  ${sdkTarget}
  PRIVATE
  $<$<CONFIG:Debug>:DEBUG>
  BUILD_TYPE="${CMAKE_BUILD_TYPE}"
  FMT_USE_STRING_VIEW
  SPDLOG_NO_EXCEPTIONS=1
  SPDLOG_WCHAR_SUPPORT=1
  SPDLOG_WCHAR_TO_UTF8_SUPPORT=1
)

TARGET_SOURCES(${sdkTarget}
  PRIVATE
  ${sdkTargetSources})

IRSDKCPP_TARGET_LINK_SDK_LIBS(${sdkTarget})

IF(NOT DEFINED CMAKE_DEBUG_POSTFIX)
  SET(CMAKE_DEBUG_POSTFIX "d")
ENDIF()

SET_TARGET_PROPERTIES(${sdkTarget} PROPERTIES
  VERSION "${PROJECT_VERSION}"
  SOVERSION "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}"
  PROJECT_LABEL "${PROJECT_NAME} ${LIB_TYPE}"
  DEBUG_POSTFIX "${CMAKE_DEBUG_POSTFIX}")

SET(EXPORT_TARGETS irsdkcpp::${sdkTarget})
CONFIGURE_PACKAGE_CONFIG_FILE(
  "${CMAKE_CURRENT_LIST_DIR}/${PROJECT_NAME}-config.cmake.in"
  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}-config.cmake"
  INSTALL_DESTINATION "${IRSDKCPP_INSTALL_CMAKEDIR}"
  PATH_VARS CMAKE_INSTALL_INCLUDEDIR CMAKE_INSTALL_LIBDIR)
UNSET(EXPORT_TARGETS)

WRITE_BASIC_PACKAGE_VERSION_FILE(
  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}-config-version.cmake"
  COMPATIBILITY AnyNewerVersion)

CONFIGURE_FILE(${PROJECT_NAME}.pc.in ${PROJECT_BINARY_DIR}/${PROJECT_NAME}.pc @ONLY)

INSTALL(TARGETS ${sdkTarget}
  EXPORT ${PROJECT_NAME}-targets
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
  FILE_SET publicHeaders)

INSTALL(
  DIRECTORY ${sdkIncludeDir}
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  FILES_MATCHING PATTERN "*.h")

INSTALL(EXPORT ${PROJECT_NAME}-targets
  NAMESPACE ${PROJECT_NAME}::
  DESTINATION "${IRSDKCPP_INSTALL_CMAKEDIR}")

INSTALL(FILES
  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}-config.cmake"
  "${PROJECT_BINARY_DIR}/${PROJECT_NAME}-config-version.cmake"
  DESTINATION "${IRSDKCPP_INSTALL_CMAKEDIR}")
INSTALL(FILES "${PROJECT_BINARY_DIR}/${PROJECT_NAME}.pc"
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig)

# Documentation Generation
IF(IRSDKCPP_BUILD_DOCS)
  SET(DOXYGEN_PROJECT_NAME irsdkcpp)
  SET(DOXYGEN_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/docs/xml)
  SET(DOXYGEN_RECURSIVE YES)
  SET(DOXYGEN_XML_OUTPUT YES)

  DOXYGEN_ADD_DOCS(
    irsdkcpp_docs
    ${CMAKE_CURRENT_LIST_DIR}/src
    ${CMAKE_CURRENT_LIST_DIR}/include
  )
ENDIF()

IF(IRSDKCPP_BUILD_TESTS)
  SET(testTargetName ${sdkTarget}_tests)
  ADD_EXECUTABLE(${testTargetName} ${sdkTestSources})

  IRSDK_CPP_CONFIGURE_TARGET(
    ${testTargetName}
  )

  TARGET_LINK_LIBRARIES(${testTargetName}
    ${sdkTarget}
    GTest::gtest GTest::gmock
  )

  GTEST_ADD_TESTS(
    TARGET ${testTargetName}
    SOURCES ${sdkTestSources}
    TEST_PREFIX ${LIB_TYPE}
  )
ENDIF()
